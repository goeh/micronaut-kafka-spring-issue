buildscript {
	ext {
		springBootVersion = '2.0.5.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

ext.buildNumber = {
    def buildNumber = System.getenv('TRAVIS_BUILD_NUMBER')
    if (buildNumber == null || buildNumber.allWhitespace) {
        buildNumber = '0.1'
    }
    return buildNumber
}

version = buildNumber()
group = 'issue'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

ext {
	springCloudVersion = 'Finchley.SR1'
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.cloud:spring-cloud-stream')
	compile('org.springframework.cloud:spring-cloud-stream-binder-kafka')
	compile('org.springframework.kafka:spring-kafka')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.cloud:spring-cloud-stream-test-support')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

task docker(dependsOn: build) {
    def dockerStageDir = new File(project.buildDir, "docker")
    def tagName = buildNumber()
    def jarName = "${project.name}-${version}.jar"
    doLast {
        copy {
            from jar
            into dockerStageDir
        }

        def buildFile = new File(dockerStageDir, "Dockerfile")
        buildFile.text = """\
FROM openjdk:8-jre-slim
ADD ${jarName} ${jarName}
RUN sh -c 'touch /${jarName}'
ENTRYPOINT ["sh", "-c", "exec java -jar \$JAVA_OPTS -Djava.security.egd=file:/dev/./urandom -Xms128m -Xmx128m /${jarName}"]
"""

        exec {
            workingDir = dockerStageDir
            commandLine 'docker', 'build', '-t', "${project.name}:${tagName}", '.'
        }
    }
}
